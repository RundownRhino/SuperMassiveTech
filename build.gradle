buildscript {
    repositories {
        maven {
			url = "https://repo1.maven.org/maven2/"
			}
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT' }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'


group = 'tterrag.supermassivetech'
archivesBaseName = "SuperMassiveTech"
version = "MC${minecraft_version}-${mod_version}-${System.getenv().BUILD_NUMBER}"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = 'run'
	
	replace "@VERSION@", project.version
	replace "${toReplace}", "required-after:Forge@[${forgeDep_version},);required-after:endercore@[${endercore_version},);after:EnderIO;after:Waila@[${WAILA_version},);"
    replaceIn "ModProps.java"
}

repositories {
    maven { // WAILA
        name "Mobius Repo"
        url "https://mobiusstrip.eu/maven"
    }
    maven { // CCC/NEI (for WAILA)
        name 'CB Repo'
        url "https://chickenbones.net/maven/"
    }
    maven { // EnderIO & EnderCore
        name 'tterrag Repo'
        url "https://maven.tterrag.com"
    }
}

dependencies {
    compile "com.enderio.core:EnderCore:${endercore_version}:dev"
    compile "mcp.mobius.waila:Waila:${WAILA_version}_${minecraft_version}:dev"
    compile ("com.enderio:EnderIO:1.7.10-2.2.8.381:dev") {
        transitive = false
    }
    compile fileTree(dir: 'libs', include: '*.jar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn sourceJar, javadocJar, deobfJar

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact javadocJar
            artifact deobfJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}
